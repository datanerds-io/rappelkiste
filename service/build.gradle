ext {
    jettyVersion = "9.3.13.v20161014"
}

dependencies {
    compile([
            project(':api'),
            "org.eclipse.jetty:jetty-server:$jettyVersion",
            "org.eclipse.jetty:jetty-servlet:$jettyVersion",
            "org.glassfish.jersey.containers:jersey-container-servlet-core:2.22.2",
            "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jacksonVersion",
            "com.squareup:tape:1.2.3"
    ])
}

task startRappelkiste(type: JavaExec) {
    description = 'This task will start a Rappelkiste on port 8080'
    main = 'io.datanerds.rappelkiste.service.Rappelkiste'
    classpath = sourceSets.main.runtimeClasspath
}

task startAsync(type: RunAsyncTask, dependsOn: build){
    taskToExecute = startRappelkiste
}

task startRappelCluster(type: JavaExec) {
    description = 'This task will start a RappelkisteCluster on the ports provided. If no ports are given it will start on port 8080 only'
    main = 'io.datanerds.rappelkiste.service.RappelCluster'
    classpath = sourceSets.main.runtimeClasspath

    if (!System.properties.ports) {
        args "8080"
    } else {
        args System.getProperty("ports").split()
    }
}

import java.util.concurrent.Callable
import java.util.concurrent.Executors

class RunAsyncTask extends DefaultTask {
    Task taskToExecute = null
    @TaskAction
    def startAsync() {
        def executor = Executors.newSingleThreadExecutor()
        executor.submit({taskToExecute.execute()} as Callable)
    }
}
